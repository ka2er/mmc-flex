<?xml version="1.0" encoding="UTF-8"?>
<mx:ApplicationControlBar xmlns:mx="http://www.adobe.com/2006/mxml"
	dock="false"
	cornerRadius="0"
	width="100%"
	paddingLeft="5"
	paddingRight="5"
	creationComplete="init()"
 	>

	<mx:Script>
	<![CDATA[
		import vo.Setting;
		import vo.Film;
		import model.Model;
		import business.events.LoadFilmsEvent;
	
		private const LABEL_NO_PATH:String = '-- Veuillez choisir un chemin --';
	
		[Bindable]		
		public var _model:Model = Model.getInstance();
		
		[Bindable]
		private var path:Array = _model.settings.pathList;
		
		/* local var to hold search parameters */
		private var filterPath:String;
		private var filterText:String;
		
		private function updateSearchFilter():void{
			filterText = search.text.toLowerCase();
			filterPath = searchPath.selectedLabel;
			
			if(filterPath == LABEL_NO_PATH) filterPath = ''; // pour pouvoir facilement supprimer le label
			
			_model.films.filterFunction = filterText || filterPath ? searchFilter : null;
			_model.films.refresh();
		}
		
		private function searchFilter(f:Film):Boolean {
			var found:Boolean = true;
			
			if(filterText)
				found = f.titre.toLowerCase().indexOf(filterText) >= 0; 
			
			if(filterPath) // les deux filtres doivent matcher
				found = found && f.path == filterPath;
			
			return found;
		}

		private function clearFilter():void{
			search.text = '';
			searchPath.selectedIndex = -1; // unselect ...
			updateSearchFilter();
		}
		
		private function init():void{
			var s:Setting = new Setting();
			s.name = LABEL_NO_PATH;
			path.unshift(s);
			searchPath.selectedIndex = 0;
		}

	]]>
	</mx:Script>	

	<mx:Label text="Search:"  />
	<mx:TextInput width="100%" maxWidth="200" 
		id="search" 
		change="updateSearchFilter()"
	/>
	<mx:ComboBox
		id="searchPath"
		dataProvider="{path}"
		labelField="name"
		change="updateSearchFilter()"
	/>
	<mx:Spacer width="100%" />
	<mx:Button click="clearFilter()" label="clear"/>
	<mx:Button click="clearFilter();new LoadFilmsEvent().dispatch()" label="refresh"/>
</mx:ApplicationControlBar>